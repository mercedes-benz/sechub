// SPDX-License-Identifier: MIT

plugins {
    // see https://github.com/JetBrains/gradle-intellij-plugin and
    // https://plugins.gradle.org/plugin/org.jetbrains.intellij
    id 'org.jetbrains.intellij' version '1.17.2'

    // https://github.com/JetBrains/gradle-changelog-plugin#initializechangelog
    id("org.jetbrains.changelog") version "2.2.1"

    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

repositories {
    // intellij plugin adds www.jetbrains.com/intellij-repository/releases in a redirected way over
    // https://cache-redirector.jetbrains.com/www.jetbrains.com/intellij-repository/releases
    mavenCentral()
}

tasks.withType(JavaCompile).configureEach {
    options.release = 17
}

// Info: https://plugins.jetbrains.com/docs/intellij/tools-gradle-intellij-plugin.html#configuration-intellij-extension
dependencies {
    implementation 'com.mercedesbenz.sechub:sechub-openapi-java-client'
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

apply plugin: 'org.jetbrains.intellij'

group 'com.mercedesbenz.sechub'
version '0.4.1'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

sourceSets{
    main{
        java{
            srcDirs 'src/main/java', 'src/main/java-intellij'
        }
    }
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    // we chose 2023.1.1 - to have compatibility to Android Studio (Hedgehog, 2023.1.1 is last stable release at the moment)
    // https://developer.android.com/studio/releases/
    // https://plugins.jetbrains.com/docs/intellij/android-studio-releases-list.html#2023
    version= '2023.1.1'
    type = 'IC'
    plugins = ['java']
}

patchPluginXml {
    sinceBuild = '231'
    untilBuild = ''
}

asciidoctor {
    baseDirFollowsSourceDir()
    sources {
        include 'user-guide.adoc', 'plugin-development.adoc'
    }
    outputDir  file('build/docs/asciidoc')

    attributes 'build-gradle': file('build.gradle'),
            'source-highlighter': 'coderay',
            'toc': 'left',
            'toclevels': '5',
            'icons': 'font',
            'setanchors': 'true',
            'idprefix': '',
            'idseparator': '-',
            'docinfo1': 'true',
            'sectnums': 'true',
            'sectnumlevels': '5',
            'version': rootProject.version
}

signPlugin {
    certificateChain = System.getenv('SECHUB_CERTIFICATE_CHAIN')
    privateKey = System.getenv('SECHUB_PRIVATE_KEY')
    password = System.getenv('SECHUB_PRIVATE_KEY_PASSWORD')
}

publishPlugin {
    token = System.getenv('SECHUB_INTELLIJ_PUBLISH_TOKEN')
}