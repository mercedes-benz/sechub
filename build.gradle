// SPDX-License-Identifier: MIT
buildscript{

	apply from: "${rootProject.projectDir}/libraries.gradle"
	apply from: "${rootProject.projectDir}/projects.gradle"

    def customMavenRepoURL4plugins = System.getenv('CUST_MVN_URL_PLUGINS')
    if (customMavenRepoURL4plugins!=null){
        repositories {
            maven { url "${customMavenRepoURL4plugins}" } // e.g. a corporate nexus or artifactory...
        }
    }else{
    	repositories {
    	     mavenCentral()
    	}
    }

	dependencies{
		classpath gradleApi()
		classpath "org.ajoberstar.grgit:grgit-gradle:4.0.1" // necessary for version calculation
		classpath "com.epages:restdocs-api-spec-gradle-plugin:${libraryVersion.restDocsApiSpec}"

	}
}
plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'org.asciidoctor.jvm.pdf' version '3.3.2'
    id 'org.openapi.generator' version '5.2.0'
    id 'org.springframework.boot' version '2.6.4' apply false
    id 'com.diffplug.spotless' version '6.2.1'
}
/* check buildsystem */
def githubActor = System.getenv('GITHUB_ACTOR')

def atGitHubActions = false

if (githubActor == null || githubActor.isEmpty()){
    /* not inside github actions */
    atGitHubActions = false
}else{
    atGitHubActions = true
}

/* define global `buildDoneByGitHubActions` - so sub projects can reuse this information*/
ext.buildDoneByGitHubActions=atGitHubActions
ext.springBootMavenBomCoordinates = org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES

ext {
    git = org.ajoberstar.grgit.Grgit.open(currentDir: project.rootDir) // necessary for version calculation
}

/* own clean task - we need this because root project has not included a module providing the task */
task internalCleanRootBuildFolder(type: Delete){

    doFirst {
        def rootBuildFolder = file("${project.projectDir}/build")
        if (! rootBuildFolder.exists()){
            rootBuildFolder.mkdirs();
        } 
        delete rootBuildFolder.listFiles() // so we do NOT clear buildSrc/build here!
    }

}

tasks.clean.dependsOn.internalCleanRootBuildFolder

allprojects {
    apply from: rootProject.file('gradle/spotless.gradle')

    def customMavenRepoURL = System.getenv('CUST_MVN_URL')
    if (customMavenRepoURL!=null){
        repositories {
            maven { url "${customMavenRepoURL}" } // e.g. a corporate nexus or artifactory...
        }
    }else{
        repositories {
             mavenCentral()
        }
    }

    /* every project has got this additional task */
    task prepareGitPush(dependsOn: spotlessApply){
    
    }
}



spotless {
	groovyGradle {
		target '*.gradle', 'gradle/*.gradle'
	}
	format 'dotfiles', {
		target '.gitignore', '.gitattributes', '.editorconfig'
		indentWithSpaces(2)
		trimTrailingWhitespace()
		endWithNewline()
	}
}

apply from: "${rootProject.projectDir}/build-versioning.gradle"
apply from: "${rootProject.projectDir}/build-java.gradle"
apply from: "${rootProject.projectDir}/build-spring.gradle"
apply from: "${rootProject.projectDir}/build-maven.gradle"
apply from: "${rootProject.projectDir}/build-eclipse.gradle"
apply from: "${rootProject.projectDir}/build-report.gradle"
