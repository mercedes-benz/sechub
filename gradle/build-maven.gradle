// SPDX-License-Identifier: MIT
/* ============================================================================
   This file contains the configurations for
   Maven settings
   ============================================================================

   Included from: "${rootProject.projectDir}/build.gradle"
   ============================================================================
 */

subprojects {


    if (!projectType.publishedLibraries.contains(project)){
        return;
    }

    // we always set the libraries version in this case
    // If we publish and the version is set, it means the dedicated, published lib version
    // If not defined, the library version is 0.0.0 which represents a snapshot -
    // the snapshot itself is related to the spring boot application (server or PDS) which
    // contains the lib (e.g. commons-core, commons-model). So it is clear there
    // could be changes to last published one.
    version = versionData.getLibrariesVersion()
    
    def libraryPublishNecessary = version != "0.0.0";
    if (! libraryPublishNecessary){
        return;
    }

    /* ensure we are at GITHUB actions when we want to publish the libraries */
    if (! buildDoneByGitHubActions ){
        println("library $project.name publish will be ignored - only provided by github actions")
        return;
    }
    

	apply plugin: 'maven-publish'
	
	publishing {

      repositories {
        maven {
          name = "GitHubPackages"
          
          url = "https://maven.pkg.github.com/mercedes-benz/sechub"
          credentials {
            username = System.getenv("GITHUB_ACTOR")
            password = System.getenv("GITHUB_TOKEN")
          }
        }
      }
      
      publications {
           mavenJava(MavenPublication) {

           from components.java

           pom {
               name = "${project.name}:${project.version}"
               description = 'This artifact is part of SecHub'

               scm {
                   url = 'https://github.com/mercedes-benz/sechub'
               }

                licenses {
                   license {
                       name = 'MIT License'
                       url = 'https://github.com/mercedes-benz/sechub/blob/master/LICENSE'
                   }
               }
           }
         }
      }
      
    }
    
}
