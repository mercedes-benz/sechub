// SPDX-License-Identifier: MIT
 /*============================================================================
 * Build file for subproject
 *
 * Root build file: "${rootProject.projectDir}/build.gradle"
 * ============================================================================
 */
plugins {
    id 'java-library'
}

version =   versionData.getPdsToolsVersion()

dependencies {

    implementation project(':sechub-commons-pds')
    implementation project(':sechub-commons-archive')
    
    // we need the next line to provide system tests via pds tools!
    implementation project(':sechub-systemtest')
    
    implementation library.apache_commons_io
    implementation library.jcommander
    
    testImplementation spring_boot_dependency.junit_jupiter
    testImplementation spring_boot_dependency.mockito_core

}


task buildPDSToolsCLI(type: Jar, dependsOn: build) {
    group 'sechub'
    description 'Builds the PDS tools as standalone executable jar'
    archiveBaseName = 'sechub-pds-tools-cli'
    
    manifest {
        attributes 'Main-Class': 'com.mercedesbenz.sechub.pds.tools.PDSToolsCLI'
    }
    
    from { 
        configurations.runtimeClasspath.collect { 
            it.isDirectory() ? it : zipTree(it) 
        } 
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    with jar
}
